plugins {
    id 'java-library'
    id 'org.bytedeco.gradle-javacpp-platform' version "1.5.4"
}

group 'ch.bildspur'
version '0.6.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://jitpack.io' }
}

configurations {
    jar.archiveName = outputName + '.jar'

    processing
    compile.extendsFrom processing
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.runtime
}

dependencies {
    // compile
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'com.github.cansik:webcam-capture-processing:8f1ea8d38d'

    // opencv (make sure you have updated the local maven repo: mvn -U compile)
    compile (group: 'org.bytedeco', name: 'javacv', version: "1.5.4"){
        exclude group: 'org.bytedeco', module: 'librealsense2'
        exclude group: 'org.bytedeco', module: 'librealsense2-platform'
    }

    compile group: 'org.bytedeco', name: 'opencv-platform', version: "4.4.0-1.5.4"
    compile group: 'org.bytedeco', name: 'openblas-platform', version: "0.3.10-1.5.4"
    compile group: 'org.bytedeco', name: 'tesseract-platform', version: "4.1.1-1.5.4"

    // cuda additions
    if (project.hasProperty("cuda")) {
        compile group: 'org.bytedeco', name: 'opencv-platform-gpu', version: "4.4.0-1.5.4"
        compile group: 'org.bytedeco', name: 'cuda-platform-redist', version: "11.0-8.0-1.5.4"
    }

    // processing
    processing group: 'org.processing', name: 'core', version: '3.3.7'
    processing group: 'org.jogamp.jogl', name: 'jogl-all-main', version: '2.3.2'
    processing group: 'org.jogamp.gluegen', name: 'gluegen-rt-main', version: '2.3.2'
}

task fatJar(type: Jar) {
    archiveName = "$outputName-complete.jar"
    dependsOn configurations.runtime
    from {
        (configurations.runtime - configurations.processing).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    with jar
}

// add processing library support
apply from: "processing-library.gradle"